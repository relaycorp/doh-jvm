import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript{
  ext {
    kotlinCoroutinesVersion = '1.6.4'
    ktorVersion = '2.2.3'
    junit5Version = '5.9.2'
    okhttpVersion = '4.10.0'
  }
}

plugins {
  id("org.jetbrains.kotlin.jvm") version "1.8.10"
  id('java-library')
  id("org.jetbrains.dokka") version "1.7.20"
  id("org.jlleitschuh.gradle.ktlint") version "11.1.0"
  id('jacoco')
  id('idea')

  id('signing')
  id('maven-publish')
  id("io.github.gradle-nexus.publish-plugin") version "1.2.0"
}

apply from: 'jacoco.gradle'
apply from: 'release.gradle'

group = "tech.relaycorp"

sourceSets {
  integrationTest {
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output
  }
}

configurations {
  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

repositories {
  mavenCentral()
}

dependencies {
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"

  implementation("io.ktor:ktor-client-okhttp:$ktorVersion")
  implementation("com.squareup.okhttp3:okhttp:$okhttpVersion")
  implementation("dnsjava:dnsjava:3.5.2")

  testImplementation("org.jetbrains.kotlin:kotlin-test")
  testImplementation("org.junit.jupiter:junit-jupiter:$junit5Version")
  testImplementation("org.junit.jupiter:junit-jupiter-params:$junit5Version")
  testImplementation("org.jetbrains.kotlin:kotlin-test-junit5")
  testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutinesVersion")
  testImplementation("com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0")
  testImplementation("org.mockito:mockito-inline:5.1.1")
  testImplementation("io.ktor:ktor-client-mock:$ktorVersion")
  testImplementation("io.ktor:ktor-client-mock-jvm:$ktorVersion")
}

kotlin {
  explicitApi()
}

java {
  withJavadocJar()
  withSourcesJar()
}

tasks.withType(KotlinCompile).configureEach {
  kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}

tasks.withType(KotlinCompile).configureEach {
  kotlinOptions.freeCompilerArgs = kotlinOptions.freeCompilerArgs + [
          "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
  ]
}

tasks.register('integrationTest', Test) {
  description = 'Integration tests'
  group = 'verification'

  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  shouldRunAfter test

  useJUnitPlatform()
}

check.dependsOn integrationTest

// Documentation

dokkaHtml.configure {
  dokkaSourceSets {
    configureEach {
      reportUndocumented.set(true)
    }
  }
}
